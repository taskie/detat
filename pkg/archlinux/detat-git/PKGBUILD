pkgname=detat-git
pkgver=0.2.0
pkgrel=1
pkgdesc="cat(1) with chardet."
arch=('x86_64')
url="https://github.com/taskie/detat"
license=('LGPL3')
provides=("detat")
makedepends=('cargo' 'git')
optdepends=()
depends=()
conflicts=('detat')
source=("$pkgname::git+https://github.com/taskie/detat")
sha1sums=('SKIP')

build() {
  cd "$pkgname"
  if command -v rustup > /dev/null 2>&1; then
    RUSTFLAGS="-C target-cpu=native" rustup run nightly \
      cargo build --release
  elif rustc --version | grep -q nightly; then
    RUSTFLAGS="-C target-cpu=native" \
      cargo build --release
  else
    cargo build --release
  fi
}

pkgver() {
  cd "$pkgname"
  local tag=$(git tag --sort=-v:refname | grep '^[0-9]' | head -1)
  local commits_since=$(git rev-list $tag..HEAD --count)
  echo "$tag.r$commits_since.$(git log --pretty=format:'%h' -n 1)"
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/detat" "$pkgdir/usr/bin/detat"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
